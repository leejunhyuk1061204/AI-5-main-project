plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'kr.co.himedia'
version = '0.0.1-SNAPSHOT'
description = 'AI기반 차량 예비 정비 시스템 백엔드'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-amqp'
	implementation 'org.springframework.boot:spring-boot-starter-batch'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.amqp:spring-rabbit-test'
	testImplementation 'org.springframework.batch:spring-batch-test'
	// AWS S3
	implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3:3.1.1'

	// Dotenv (Read .env files)
	implementation 'me.paulschwarz:spring-dotenv:5.0.1'

	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

	// QueryDSL
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	// Smartcar SDK
	// OkHttp for High Mobility
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// Google Cloud Vision
	implementation 'com.google.cloud:google-cloud-vision:3.32.0'

	// Firebase Admin SDK
	implementation 'com.google.firebase:firebase-admin:9.2.0'

	// Retry & AOP
	implementation 'org.springframework.retry:spring-retry'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
}

tasks.named('test') {
	useJUnitPlatform()
}

// 루트 폴더의 .env 파일을 환경 변수로 주입
tasks.withType(JavaExec) {
    if (file("../.env").exists()) {
        file("../.env").readLines().each { line ->
            if (!line.trim().isEmpty() && !line.startsWith("#")) {
                def parts = line.split("=", 2)
                if (parts.length == 2) {
                    def key = parts[0].trim()
                    def value = parts[1].trim()
                    environment key, value
                }
            }
        }
    }
}

// Spring Boot Run 설정: .env 위치 명시 + 환경변수 주입
bootRun {
    systemProperty "dotenv.location", "../"
    
    // .env 파일에서 환경변수 직접 주입
    if (file("../.env").exists()) {
        file("../.env").readLines().each { line ->
            if (!line.trim().isEmpty() && !line.startsWith("#")) {
                def parts = line.split("=", 2)
                if (parts.length == 2) {
                    def key = parts[0].trim()
                    def value = parts[1].trim()
                    // 주석 제거 (값 뒤의 # 이후 내용)
                    if (value.contains("#")) {
                        value = value.split("#")[0].trim()
                    }
                    environment key, value
                }
            }
        }
    }
}
